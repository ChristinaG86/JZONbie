dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.8.7'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.8.7'
    testCompile project(':jzonbie-test-util')
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.5.2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.6.8'
}

ext {
    jarName = 'jzonbie-core'
}

jar {
    baseName jarName
    manifest {
        attributes "application-version": version
    }
}

javadoc {
    source = sourceSets.main.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName jarName
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName jarName
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

// maven-publish plugin configuration
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// signing and maven plugin configuration. Run gradle with project property -Prelease to use these tasks
// Ensure gradle.properties are set for signing key and sonatype credentials
if (project.hasProperty("release")) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'JZONbie'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    artifactId jarName
                    description 'HTTP client for JZONbie'
                    url 'jonnymatts.com'

                    scm {
                        connection 'scm:git:git://github.com/jonnymatts/jzonbie.git'
                        developerConnection 'scm:git:ssh://github.com:jonnymatts/jzonbie.git'
                        url 'http://github.com/jonnymatts/jzonbie/tree/master'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            name 'Jonathan Matts'
                            email 'jonoamatts@gmail.com'
                        }
                    }
                }
            }
        }
    }
}